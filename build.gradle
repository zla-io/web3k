buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0' // generate a POM file
    }
}

subprojects {
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }

    test {
        useJUnitPlatform {
            include 'spek'
        }
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
        testCompile "org.jetbrains.spek:spek-api:$spek_version"
        testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
        testCompile "org.mockito:mockito-core:2.13.0"
        testCompile "com.nhaarman:mockito-kotlin:1.5.0"
        testCompile "org.assertj:assertj-core:3.8.0"
    }

    apply from: "$rootDir/gradle/publish.gradle"
    apply plugin: 'maven-publish'
    apply from: "$rootDir/gradle/bintray.gradle"

    group = publishedGroupId

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }
    }
}
